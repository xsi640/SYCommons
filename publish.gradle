task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set("sources")
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set("javadoc")
}

publishing {
    // 定义发布什么
    publications {
        mavenJava(MavenPublication) {
//            groupId = "com.github.xsi640"
//            artifactId = "SYCommons"
//            version = project.version
            // groupId,artifactId,version，如果不定义，则会按照以上默认值执行
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                // 构件名称
                // 区别于artifactId，可以理解为artifactName
                name = 'sy-commons'
                // 构件描述
                description = 'sy Common library'
                // 构件主页
                url = 'https://github.com/xsi640/SYCommons'
                // 许可证名称和地址
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                // 开发者信息
                developers {
                    developer {
                        name = 'xsi640'
                        email = 'xsi64@126.com'
                    }
                }
                // 版本控制仓库地址
                scm {
                    url = 'https://github.com/xsi640/SYCommons'
                    connection = 'scm:git:git://github.com/xsi640/SYCommons.git'
                    developerConnection = 'scm:git:ssh://git@github.com:xsi640/SYCommons.git'
                }
            }
        }
    }
    // 定义发布到哪里
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                // 这里就是之前在issues.sonatype.org注册的账号
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    // <meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
    // 防止本地打开中文乱码
    options.addStringOption("charset", "UTF-8")
}